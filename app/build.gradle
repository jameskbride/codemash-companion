repositories {
    jcenter()
    maven { url 'https://maven.google.com' }
    maven { url "https://jitpack.io" }
}

apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.jameskbride.codemashcompanion"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 8
        versionName "1.5.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
    }
    signingConfigs {
        debug {
            storeFile file(System.getenv("KEYSTORE_DEBUG"))
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            Properties properties = new Properties()
            properties.load(project.rootProject.file('release.properties').newDataInputStream())
            storeFile file(properties.getProperty('store.file'))
            storePassword properties.getProperty("store.password")
            keyAlias properties.getProperty("key.alias")
            keyPassword properties.getProperty("key.password")
        }
    }
    buildTypes {
        debug {
            signingConfig debug.signingConfig
            debuggable true
            buildConfigField "String", "BASE_URL", '"http://10.0.2.2:8181/"'
        }
        qa {
            signingConfig debug.signingConfig
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "BASE_URL", '"https://sessionize.com/"'
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "BASE_URL", '"https://sessionize.com/"'
            buildConfigField "String", "BASE_URL", '"https://sessionize.com/"'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
        androidTest.assets.srcDirs +=
                files("$projectDir/schemas".toString())
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha01'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation 'com.google.dagger:dagger:2.13'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.1'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.6'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.1.0'
    implementation "androidx.room:room-runtime:2.1.0-alpha03"
    implementation 'com.google.android.material:material:1.1.0-alpha01'
    implementation "androidx.room:room-rxjava2:2.1.0-alpha03"
    implementation "com.squareup.picasso:picasso:2.5.2"
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0-alpha01'
    implementation 'com.github.chrisbanes:PhotoView:2.1.3'
    implementation 'com.xwray:groupie:2.3.0'
    implementation 'com.xwray:groupie-kotlin-android-extensions:2.3.0'
    kapt 'com.google.dagger:dagger-compiler:2.13'
    kapt "androidx.room:room-compiler:2.1.0-alpha03"
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.12.0'
    testImplementation 'com.squareup.okhttp3:mockwebserver:3.9.1'
    testImplementation "com.nhaarman:mockito-kotlin-kt1.1:1.5.0"
    androidTestImplementation 'androidx.test:runner:1.1.1-beta01'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1-beta01'
    androidTestImplementation "androidx.annotation:annotation:1.0.1"
    androidTestImplementation "androidx.test:rules:1.1.1-beta01"
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    testImplementation "androidx.room:room-testing:2.1.0-alpha03"
}
