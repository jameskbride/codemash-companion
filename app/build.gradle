repositories {
    jcenter()
    maven { url 'https://maven.google.com' }
    maven { url "https://jitpack.io" }
}

apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.jameskbride.codemashcompanion"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 6
        versionName "1.3.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
    }
    signingConfigs {
        release {
            Properties properties = new Properties()
            properties.load(project.rootProject.file('release.properties').newDataInputStream())
            storeFile file(properties.getProperty('store.file'))
            storePassword properties.getProperty("store.password")
            keyAlias properties.getProperty("key.alias")
            keyPassword properties.getProperty("key.password")
        }
    }
    buildTypes {
        debug {
            buildConfigField "String", "BASE_URL", '"http://10.0.2.2:8181/"'
        }
        qa {
            signingConfig signingConfigs.debug
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "BASE_URL", '"https://speakers.codemash.org/"'
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "BASE_URL", '"https://speakers.codemash.org/"'
            buildConfigField "String", "BASE_URL", '"https://speakers.codemash.org/"'
        }
    }
    sourceSets {
        androidTest.assets.srcDirs +=
                files("$projectDir/schemas".toString())
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:27.0.2'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation 'com.google.dagger:dagger:2.13'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.1'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.6'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.1.0'
    implementation "android.arch.persistence.room:runtime:1.0.0"
    implementation 'com.android.support:design:27.0.2'
    implementation "android.arch.persistence.room:rxjava2:1.0.0"
    implementation "com.squareup.picasso:picasso:2.5.2"
    implementation 'com.android.support:cardview-v7:27.0.2'
    implementation 'com.android.support:recyclerview-v7:27.0.2'
    implementation 'com.github.chrisbanes:PhotoView:2.1.3'
    kapt 'com.google.dagger:dagger-compiler:2.13'
    kapt "android.arch.persistence.room:compiler:1.0.0"
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.12.0'
    testImplementation 'com.squareup.okhttp3:mockwebserver:3.9.1'
    testImplementation "com.nhaarman:mockito-kotlin-kt1.1:1.5.0"
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    androidTestImplementation "com.android.support:support-annotations:27.0.2"
    androidTestImplementation "com.android.support.test:rules:1.0.1"
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    testImplementation "android.arch.persistence.room:testing:1.0.0"
}
